plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.sonarqube' version '7.2.2.6593'
    id 'jacoco'
}

group = 'site.holliverse'
version = '0.0.1-SNAPSHOT'
description = 'worker'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}


repositories {
    mavenCentral()
}

/* test setup */
def jacocoExcludes = [
        '**/*Application*',
        '**/*Dto*',
        '**/*Vo*',
        '**/*Request*',
        '**/*Exception*',
        '**/*Response*'
]

sonar {
    properties {
        property "sonar.projectKey", "api-server-test"
        property "sonar.organization", "one-year-gap"
        property "sonar.host.url", "https://sonarcloud.io"

        property "sonar.coverage.jacoco.xmlReportPaths",
                "${layout.buildDirectory.get().asFile}/reports/jacoco/test/jacocoTestReport.xml"
    }
}

jacoco {
    toolVersion = "0.8.14"
}

tasks.test {
    useJUnitPlatform()
    finalizedBy tasks.jacocoTestReport
}

tasks.jacocoTestReport {
    dependsOn tasks.test

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)

        xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco/test/jacocoTestReport.xml"))
        html.outputLocation.set(layout.buildDirectory.dir("reports/jacoco/test/html"))
    }

    classDirectories.setFrom(files(classDirectories.files.collect { dir ->
        fileTree(dir: dir, exclude: jacocoExcludes)
    }))

    finalizedBy tasks.jacocoTestCoverageVerification
}


tasks.named("sonarqube") {
    dependsOn(tasks.jacocoTestReport)
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            enabled = true
            element = 'BUNDLE'

            limit {
                counter = 'BRANCH'
                value = 'COVEREDRATIO'
                //minimum = 0.50
            }

            limit {
                counter = 'LINE'
                value = 'COVEREDRATIO'
                //minimum = 0.70/
            }
        }
    }
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-batch'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.batch:spring-batch-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
    useJUnitPlatform()
}
